# 10章

RPN式 10 4 3 + 2 * - をどうやって評価するか？

1. まず、10をスタックに罪、スタックは10が1つ入った状態いｎなる。
2. 次のアイテムは4なので、これもスタックに積む。いまのスタックは10,4。
3. 3にも同じことをして、スタックは10,4,3になる
4. 演算子が出てきたのでスタックから2つの数を取り出し、取り出した2つの数を加算し、結果をスタックに戻す。スタックは10,7になる
5. 次にスタックに2を積んで、スタックは10,7,2になる
6. また演算子が北ので、7,2をスタックから取り出して、掛け算して、結果をスタックに戻す。7 * 2 は14なので、スタックは10,14になる
7. 最後に、- がある。10と14をスタックから取り出し、10から14を引いて、スタックに戻す
8. スタックに乗っている数は-4。与えられた式には、もう数も演算子も残ってないのでこれが解。

### RPN関数を書く

"10 4 3 + 2 * -" のような RPN式を文字列で受け取って、その式の結果を返す関数を書く。
この関数の型は以下のようになる。

```haskell
solveRPN :: String -> Double
solveRPN expression = head (fold1 foldingFunction [] (words expression))
    where foldingFunction stack item = ...
```
    
- expression を取って、まずアイテムのリストに変換
- アイテムのリストを関数 foldingFunction で畳み込む
- ここで [] はアキュムレータの初期値
- 単一要素の入った最終状態のスタックを受け取ったら、head で取り出す

ポイントフリースタイルで書き直す。

```haskell
solveRPN :: String -> Double
solveRPN = head . fold1 foldingFunction [] . words
    where foldingFunction stack item = ...
```
 
 畳み込み関数は、スタックとアイテムを取って新しいスタックを返すようにする。
 
 ```haskell
solveRPN :: String -> Double
solveRPN = head . foldl foldingFunction [] . words
    where foldingFunction (x:y:ys) "*" = (y * x):ys
          foldingFunction (x:y:ys) "+" = (y + x):ys
          foldingFunction (x:y:ys) "-" = (y - x):ys
          foldingFunction xs numberString = read numberString:xs
```

- パターンは上から下へ順番に試される
- 畳み込みは現在のアイテムが "*" かどうか調べそうなら [3,4,9,3] のようなリストを取り、その先頭の2つの要素をそれぞれ x と y と名付ける
- この場合だと x が 3 で y が 4 になり、ys は [9, 3] になる
- 畳み込み関数は ys の頭に 「x掛けるy」を付けて返す
- アイテムが既知の演算子でなかったら、それは数を表す文字列だと仮定する。
- もし本当に数だったら、read を適用すれば中身がが実数に変換できるはず。

```haskell
ghci>solveRPN "2 3.5 +"
5.5
ghci>solveRPN "2 3.5 +"
5.5
ghci>solveRPN "90 34 12 33 55 66 + * - +"
-3947.0
ghci>solveRPN "90 34 12 33 55 66 + * - + -"
4037.0
ghci>solveRPN "90 3.8 -"
```

##### 演算子を追加しよう

- 数を一つだけ取り出してその対数を積む。
- 可変長の引数を取る演算子を作る

sum はスタックから全ての数を取り出して総和を積む演算。

```haskell
solveRPN :: String -> Double
solveRPN = head . foldl foldingFunction [] . words
    where foldingFunction (x:y:ys) "*" = (y * x):ys
          foldingFunction (x:y:ys) "+" = (y + x):ys
          foldingFunction (x:y:ys) "-" = (y - x):ys
          foldingFunction (x:y:ys) "/" = (y / x):ys
          foldingFunction (x:y:ys) "^" = (y ** x):ys
          foldingFunction (x:ys) "1n" = log x:ys
          foldingFunction xs "sum" = [sum xs]
          foldingFunction xs numberString = read numberString:xs
```

```haskell
ghci>solveRPN "10 10 10 10 sum 4 /"
10.0
ghci>solveRPN "10 10 10 10 10 sum 4 /"
12.5
ghci>solveRPN "10 2 ^"
100.0
```

### ヒースロー空港からロンドンへ

２つの幹線道路のそれぞれの交差点までの最短経路を割り出して計算する。

##### 道路網を Haskell で表現する

- 幹線道路Aのあるパーツ
- 幹線道路Bの同じパーツ
- 橋渡しをしているパーツC

この３つの組を「Section」 と呼ぶ

```haskell
data Section = Section { getA :: Int, getB :: Int, getC :: Int }
  deriving (Show)

type RoadSystem = [Section]
```

ヒースロー空港からロンドンまでの道路網はこう表せる

```haskell
heathrowToLondon :: RoadSystem
heathrowToLondon = [ Section 50 10 30
  ,Section 5 90 20
  ,Section 40 2 25
  ,Section 10 8 0 ]
```

あとは Haskell の解を実装するだけ。

```haskell
data Label = A | B | C deriving (Eq, Show)
type Path = [(Label, Int)]
```

これから作る optimalPath の型は以下のようになる。

```haskell
optimalPath :: RoadSystem -> Path
```

heathrowToLondon という名の道路網を引数に呼び出したら、こういう経路を返すべき。

`[(B, 10), (C, 30), (A, 5), (C, 20), (B, 2), (B, 8)]`

この関数を実装してみる。

```haskell
roadStep :: (Path, Path) -> Section -> (Path, Path)
roadStep (pathA, pathB) (Section a b c) =
    let timeA = sum (map snd pathA)
        timeB = sum (map snd pathB)
        forwardTimeToA = timeA + a
        crossTimeToA = timeB + b + c
        forwardTimeToB = timeB + b
        crossTimeToB = timeA + a + c
        newPathToA = if forwardTimeToA <= crossTimeToA
                    then (A, a):pathA
                    else (C, c):(B, b):pathB
        newPathToB = if forwardTimeToB <= crossTimeToB
                    then (B, b):pathB
                    else (C, c):(A, a):pathA
    in (newPathToA, newPathToB) 
```

最短経路を求める関数

```haskell
optimalPath :: RoadSystem -> Path
optimalPath roadSystem =
    let (bestAPath, bestBPath) = foldl roadStep ([], []) roadSystem
    in  if sum (map snd bestAPath) <= sum (map snd bestBPath)
            then reverse bestAPath
            else reverse bestBPath
```

```haskell
data Section = Section { getA :: Int, getB :: Int, getC :: Int }
  deriving (Show)

type RoadSystem = [Section]

heathrowToLondon :: RoadSystem
heathrowToLondon = [ Section 50 10 30
  ,Section 5 90 20
  ,Section 40 2 25
  ,Section 10 8 0 ]

data Label = A | B | C deriving (Eq, Show)
type Path = [(Label, Int)]

roadStep :: (Path, Path) -> Section -> (Path, Path)
roadStep (pathA, pathB) (Section a b c) =
    let timeA = sum (map snd pathA)
        timeB = sum (map snd pathB)
        forwardTimeToA = timeA + a
        crossTimeToA = timeB + b + c
        forwardTimeToB = timeB + b
        crossTimeToB = timeA + a + c
        newPathToA = if forwardTimeToA <= crossTimeToA
                    then (A, a):pathA
                    else (C, c):(B, b):pathB
        newPathToB = if forwardTimeToB <= crossTimeToB
                    then (B, b):pathB
                    else (C, c):(A, a):pathA
    in (newPathToA, newPathToB) 

    
optimalPath :: RoadSystem -> Path
optimalPath roadSystem =
    let (bestAPath, bestBPath) = foldl roadStep ([], []) roadSystem
    in  if sum (map snd bestAPath) <= sum (map snd bestBPath)
            then reverse bestAPath
            else reverse bestBPath
```

##### 入力から道路網を受け取る

数値のリストを受け取り、ある要素のグループ毎に分裂する関数

```haskell
groupsOf :: Int -> [a] -> [[a]]
groupsOf 0 _ = undefined
groupsOf _ [] = []
groupsOf n xs = take n xs : groupsOf n (drop n xs)
```

[1..10] という引数に対し、`groupsOf 3` の結果はこうなるはず。

`[[1,2,3],[4,5,6],[7,8,9],[10]]`

最短経路を出力するメイン関数

```haskell
import Data.List

main = do
    contents <- getContents
    let threes = groupsOf 3 (map read $ lines contents)
        roadSystem = map (\[a,b,c] -> Section a b c) threes
        path = optimalPath roadSystem
        pathString = concat $ map (show .fst) path
        pathTime = sum $ map snd path
    putStrLn $ "The best path to take is: " ++ pathString
    putStrLn $ "Time taken: " ++ show pathTime
```

paths.txt ファイルを以下の内容で作成する

```txt
50
10
30
5
90
20
40
2
25
10
8
0
```

`$ runhaskell heathrow.hs < paths.txt` で実行。

スタックオーバーフローが起きたら、foldl を foldl' に変え、sum を foldl' (+) 0 に帰る。
また実行するまえに以下のようにコンパイルする。

`$ ghc --make -O heathrow.hs`

コンパイル時に -O フラグを含めると最適化が働いて、 foldl や sum といった関数がスタックオーバーフローしにくくなる。

