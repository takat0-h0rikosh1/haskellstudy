# 11章 ファンクターからアプリカティブファンクターへ

### 帰ってきたファンクター

ファンクターとは関数で写せるもの。
例えば、リスト、Maybe、木など。
ファンクターの型クラスメソッドは fmap のみ。

```haskell
fmap :: (a -> b) -> f a -> f b
```

「a を撮って b を返す関数」と a の入った箱を渡して、箱に入った b を返す。

型コンストラクタを Functor のインスタンスにするには、その型コンストラクタの種類(kind)は、
`* -> *` でなければならない。

つまりその型コンストラクタは型変数として一つだけ具体型を取る必要がある。

Either を Functor として扱うためには部分適応してやらなければならない。

`instance Functor Either where` は間違い。
`instance Functor (Either a) where` は正しいインスタンス宣言。

fmap は Either a に働くと考えれば次のような型になることがわかる。

`fmap :: (b -> c) -> Either a b -> Either a c`

Either a は不変であり、Either a のただ一つの変数の部分が変化している。

##### ファンクターとしてのI/Oアクション

IO String という型は、実行すると、外の世界へ出かけていって文字をとってきてそれを返してくれるようなI/Oアクションを表している。
取得結果は、do記法のなかで <- 構文をつかて名前に束縛できる。

IOはファンクターなので結果をIOの中に戻してやらなければならない。

IOの Functor インスタンスの定義を見てみる。
ある関数をある I/O アクションに fmap すると「元のI/Oと同じことをしつつ、その結果に指定した関数を適用して返す」I/Oアクションになってほしいわけである。

```haskell
instance Functor IO where
    fmap f action = do
        result <- action
        return (f result)
```

I/Oアクションを関数で写した結果もまた I/O アクションなので do 構文を使う。

1. 元々の I/O アクションを実行して結果を result と名付ける。
2. 次に return を行う。

return は「特に仕事を行わず、ただ何かを結果として提示する I/O アクション」を作る関数。

```haskell
main = do line <- getLine
          let line' = reverse line
          putStrLn $ "You said " ++ line' ++ " backwards!"
          putStrLn $ "Yes, you said " ++ line' ++ " backwards!"
```

ユーザーに文字列を入力してもらい、それを逆順に表示している。
`fmap` を使うと以下のように書ける。

```haskell
main = do line <- fmap reverse getLine
          putStrLn $ "You said " ++ line' ++ " backwards!"
          putStrLn $ "Yes, you said " ++ line' ++ " backwards!"
```

sample

```haskell
main = do
    r <- fmap (++"!") $ return "hello world"
    putStrLn r 
```

仮に fmap が IO に限定されていたら、 fmap の型は `fmap :: (a -> b) -> IO a -> IO b` になる。

もし自分のコードに何らかの関数にわたすためだけに I/O の結果に名前をつけている箇所があったら、fmap を使ってみてほしい。  
そのほうがきれいに書ける。

もしファンクターの中身を、1つでなく複数の関数を使って写したいなから、そのための関数をトップレベルで宣言してもいいし、
ラムダ式を使ってもいいが、一番いいのは関数合成。

```haskell
import Data.Char
import Data.List

main = do line <- fmap (intersperse '-' . reverse . map toUpper) getLine
          putStrLn line
          
-- 以下のように書いても同じ
-- (\xs -> intersperse '-' (reverse (map toUpper xs)))
```

##### ファンクターとしての関数

関数の型を表す r -> a は (->) r a と書き換えることができる。
2 + 3 を (+) 2 3 と表現できるのと同様。

関数の型を (->) r a と表現するとき、関数 (->) には、2つの型引数を取る型コンストラクタという新しい姿見えてくる。
Either と同様。

### ファンクター則

##### 第一法則

「id でファンクターを写した場合、ファンクター値が変化してはいけない」というもの。  
id は恒等写像、引数をそのまま返す関数。

```haskell
ghci>id 1
1
ghci>fmap id (Just 3)
Just 3
ghci>fmap id [1..5]
[1,2,3,4,5]
ghci>fmap id []
[]
ghci>fmap id Nothing
Nothing
```

##### 第二法則

関数合成と写す操作との間の関係。  
「f と g の合成関数でファンクター値を写したもの」と、  
「まず g、次に f でファンクター値を写したもの」が等しいことを要求する。

fmap (f . g) = fmap f . fmap g ということ

言い換えるとすべてのファンクター値 x に対し、

fmap (f .g) x = fmap f . (fmap g x) が成り立つべしということ。

##### 法則を破る

Functor なのに、則を満たしてない病的な例を考える。

```haskell
data CMaybe a = CNothing | CJust Int a deriving (Show)
```

C は 「counter」のつもり
CMaybe a は、Maybe a によく似たデータ型だが、Just 部分のフィールドが一つではなく2つある。
CJust の1つ目のフィールドは常に Int 型で、これが何らかのカウンタになる。
2つ目のフィールドの型 a は型引数で、a の型が何になるかわ CMaybe a をどんな具体型いしたいかによってきまる。

```haskell
ghci>CNothing
CNothing
ghci>CJust 0 "hahaha"
CJust 0 "hahaha"
ghci>:t CNothing
CNothing :: CMaybe a
ghci>:t CJust 0 "haha"
CJust 0 "haha" :: CMaybe [Char]
ghci>CJust 100 [1,2,3]
CJust 100 [1,2,3]
```

```haskell
instance Functor CMaybe where
    fmap f CNothing = CNothing
    fmap f (CJust counter x) = CJust (counter+1) (f x)
```

```haskell
ghci>fmap id (CJust 0 "haha")
CJust 1 "haha"
ghci>id (CJust 0 "haha")
CJust 0 "haha"
```

id ファンクターを写した結果と単に id をファンクター値に適用した結果とが等しくなければならない。  
よって、CMaybe はこの性質を満たさない。

### アプリカティブファンクターを使おう

```haskell
ghci>let a = fmap (*) [1,2,3,4]
ghci>:t a
a :: Num a => [a -> a]
ghci>fmap (\f -> f 9) a
[9,18,27,36]
ghci>fmap ($9) a
[9,18,27,36]
```

### Applicative ちゃんと仲良くしてあげてね！

型クラス Applicative は、2つの関数 pure と <*> を定義している。  
どちらもデフォルト実装は与えられてないので、ある型を Applicative のインスタンスにしたかったら、
両方の定義を与える必要がある。クラス定義はこんな感じ。

```haskell
class (Functor f) => Applicative f where
    pure :: a -> f a
    (<*>) :: f (a -> b) -> f a ->  f b
```

##### Maybe はアプリカティブファンクター

```haskell
ghci>Just (+3) <*> Just 9
Just 12
ghci>pure (+3) <*> Just 10
Just 13
ghci>Just (++"hahaha") <*> Nothing
Nothing
ghci>Nothing <*> Just "woot"
Nothing
```

functor は一旦関数で写したら、ファンクターの中に入ってしまった関数適用の結果をファンクターの外に取り出す一般的な方法はない。
一方アプリカティブファンクターなら、1つの関数で複数のファンクターを続けざまに写せる。

##### アプリカティブスタイル

<*> を連続して使う。

```haskell
ghci>pure (+) <*> Just 3 <*> Just 5
Just 8
ghci>pure (+) <*> Just 3 <*> Nothing
Nothing
ghci>pure (+) <*> Nothing <*> Just 5
Nothing
```

+ をアプリカティブ値の中に入れ、さらに `<*>` を使って2つの引数に適応しているが、どちらもアプリカティブ値。
`<*>` は左結合なので、

```haskell
pure (+) <*> Just 3 <*> Just 5

-- この式は、以下と同じ
(pure (+) <*> Just 3) <*> Just 5
```

- まず、pure (+) をして Just (+) を作る。
- 次に Just (+) <*> Just 3 が評価され、結果が Just (3+) となる。 -> 部分適用が起きてる
- 最後に Just (3+) <*> Just 5 が実行されて、結果が Just 8 となる。

アプリカティブファンクターとアプリカティブスタイル、
`pure f <*> x <*> y <*> ...` を使えばもともとアプリカティブなんて知らずに書かれた普通の関数にも、
アプリカティブ値の引数を与えることができる。

`pure f <*> x` は `fmap f x` と等しい。これはアプリカティブ則の一つ。

pure は値をデフォルトの文脈の中に入れる。  
ある関数をデフォルトの文脈の中に入れ、また取り出して別のアプリカティブファンクターの中の値に適応する。
この操作は、単に別の関数でアプリカティブファンクターを写すのと同じこと。

そこで、`pure f <*> x <*> y <*> ...` と書く代わりに、`f map f x <*> y <*> ...` と書くことができる。
このパターンはしょっちゅう使うので、 Control.Applicative は fmap と等価な中置演算子 `<$>` をエクスポートしている。
`<$>` の定義は以下。

```haskell
(<$>) :: (Functor f) => (a -> b) f a -> f b
f <$> x = fmap f x
```

関数 f を3つのアプリカティブ値の引数に適用したければ、 `f <$> x <*> y <*> z` と書くことができる。
例えば Just "johntra" と Just "volta" を結合して１つの Maybe String 型の値を作りたいとする。
以下のように書くことができる。

```haskell
ghci>(++) <$> Just "johntra" <*> Just "volta"
Just "johntravolta"
```

普通の関数をアプリカティブファンクターの関数として使うには、`<$>` と `<*>` を散りばめるだけ。
すると関数は、アプリカティブ値を取ってアプリカティブ値を返すようになる。

##### リスト

リストもアプリカティブファンクター。
[] のアプリカティブインスタンス宣言。

```haskell
instance Applicative [] where
    pure x = [x]
    fs <*> xs = [f x | f <- fs, x <- xs]
 ```
 
```haskell
ghci>pure "Hey" :: [String]
["Hey"]

ghci>pure "Hey" :: Maybe String
Just "Hey"

ghci> [(*0),(+100),(^2)] <*> [1,2,3]
[0,0,0,101,102,103,1,4,9]

ghci>(++) <$> ["ha","heh","hmm"] <*> ["?","!","."]
["ha?","ha!","ha.","heh?","heh!","heh.","hmm?","hmm!","hmm."]

ghci>[ x*y | x <- [2,5,10], y <- [8,10,11] ]
[16,20,22,40,50,55,80,100,110]

ghci>(*) <$> [2,5,10] <*> [8,10,11]
[16,20,22,40,50,55,80,100,110]

ghci>filter (>50) $ (*) <$>  [2,5,10] <*> [8,10,11]
[55,80,100,110]
```

##### IOもアプリカティブファンクター

```haskell
instance Applicative IO where
    pure = return
    a <*> b = do
      f <- a
      x <- b
      return (f x)
```

pure というのは要するに、再現できるような形で最小の文脈に値を入れるという意味。
なので return は何もしない I/O アクションを作る。

ただ I/O アクションを返すだけでターミナルに印字したりファイルを読み込んだりといった入出力操作は一切しない。

`<*>`  が特殊化されたら、その型は、  
`(<*>) :: IO (a -> b) -> IO a -> IO b` になるはず。

Maybe と [] に関しては、 `<*>` 演算子は単に左辺の引数から関数を取り出しては右辺を移す操作だと解釈できた。
IOに関する `<*>` 演算子は、取り出すところは同じだが、2つの I/O アクションを1つに糊付けするにあたって逐次実行という意味が新たに加わっている。

`<*>` 演算子は、まず１つ目の I/O アクションから関数を取り出すが、結果を取り出したかったらその I/O を実行する必要がある。

```haskell
myAction :: IO String
myAction = do
    a <- getLine
    b <- getLine
    return $ a ++ b
```

このプログラムは次のようにアプリカティブスタイルで書くこともできる。

```haskell
myAction :: IO String
myAction = (++) <$> getLine <*> getLine
```

この式は String を返す他の I/O アクションと全く同様、ごく普通の I/O アクションなので以下のような書き方もできる。

```haskell
main = do
    a <- (++) <$> getLine <*> getLine
    putStrLn $ "The two lines concatenated turn out to be: " ++ a
```

##### 関数もアプリカティブだよ

```haskell
instance Applicative ((->) r) where
    pure x = (\_ -> x)
    f <*> g = \x -> f x (g x)
```

pure は受け取った値を常に返す関数を作る。

```haskell
ghci> pure 3
3
ghci> pure 3 "hoge"
3
```

`<*>` は少々暗号じみているので、関数をアプリカティブスタイルで使う方法から見ていく。

```haskell
ghci>:t (+) <$> (+3) <*> (*100)
(+) <$> (+3) <*> (*100) :: Num b => b -> b
ghci>(+) <$> (+3) <*> (*100) $ 5
508
```

`<*>` を２つのアプリカティブ値に対して呼び出した結果はアプリカティブ値。
したがって、`<*>` を２つの関数に使ったら関数が返ってくる。では、このコードでは何が起こっているのか。

`(+) <$> (+3) ＜＊＞ (*100) $ 5` という形で呼ぶと、  
まず (+3) と (*100) が 5 に適用され、それぞれ 8 と 500 を返す。
それから + が引数 8 と 500 を取って呼ばれ、508 を生み出す。

次のコードも似たようなものである。

```haskell
ghci> (\x y z -> [x, y, z]) <$> (+3) <*> (*2) <*> (/2) $ 5
[8.0,10.0,2.5]
```

3つの関数 (+3)、(*2)、(/2) からの結果をもって関数 `\x y z -> [x, y, z]` を呼び出す関数。
最後の引数５は、まず３つの関数にそれぞれ入り、出てきた返り値を引数にして `\x y z -> [x, y, z]` が呼び出されている。

##### Zipリスト

通常のアプリカティブリストは、
`[(+3) (*2)] <*> [1, 2]` というふうに書いたら、(+3) は 1 と 2 の両方に適用され、  
(*2) も 1 と 2 の両方に適用されるので、結果として４つの要素からなるリスト [4,5,2,4] ができる。

左辺の関数を対応する右辺の関数に適用して [4,4] を生成するのはダメなのか？ 
これは [1 + 3, 2 * 2] をやるとも考えられる。

これをやる Applicative のインスタンスが ZipList（Control.Applicative モジュールにある）

```haskell
instance Applicative ZipList where
    pure x = ZipList (repeat x)
    ZipList fs <*> ZipList xs = ZipList (zipWith (\f x -> f x) fs fx)
```

`<*>` は、1つ目の関数を二つ目の１つ目の値に、2つ目の関数を二つ目の値に、...と適用する。
これをやっているのが `zipWith (\f x -> f x) fs xs` である。

zipWith の仕様上、出来上がるリストは２つのリストのうち短いほうの長さになる。

ZipList の pure は引数を取ってそれを永遠に繰り返すリストに突っ込む。例えば pure "nya" は、
ZipList (["nya", "nya", "nya"...]) を返す。これはおかしいと思うかもしれないが、
pure は引数を再現できる最小の文脈に入れるものなのに、無限リストはとても最小の文脈には見えない。
しかし ZipList に限っては、pure はリストのあらゆる位置で値を再現してほしいので、この定義でよい。
この定義であれば、 `pure f <*> xs` と `fmap f xs` は等価であれ、という法則を満たす。
もし、`pure 3` がただの `ZipList [3]` を返す仕様だったら `pure (*2) <*> ZipList [1, 5, 10]` の結果は
`ZipList [2]` になってしまう。2つのリストを zip した結果の長さは短い方の長さになるからである。
一方、無限リストと有限リストを zip した結果のリストの長さは、有限リストの方の長さになる。

さて、ZipList をアプリカティブスタイルで試してみる

```haskell
ghci>import Control.Applicative
ghci>getZipList $ (+) <$> ZipList [1,2,3] <*> ZipList [100,100,100]
[101,102,103]
ghci>getZipList $ (+) <$> ZipList [1,2,3] <*> ZipList [100,100..]
[101,102,103]
ghci>getZipList $ max <$> ZipList [1,2,3,4,5,3] <*> ZipList [5,3,1,2]
[5,3,3,4]
ghci>getZipList $ (,,) <$> ZipList "dog" <*> ZipList "cat" <*> ZipList "rat"
[('d','c','r'),('o','a','a'),('g','t','t')]
ghci>:t (,,)
(,,) :: a -> b -> c -> (a, b, c)
```

#### アプリカティブ則

アプリカティブファンクターにもいくつかの法則がついてくる。
中でも一番重要なのは `pure f <*> x = fmap f x`

これ以外のアプリカティブ則の一覧はこちら。

+ `pure id <*> v = v`
+ `pure (.) <*> u <*> v ()<*> w = u <*> (v <*> w)`
+ `pure f <*> pure x = pure (f x)`
+ `u <*> pure y = pure ($ y) <*> u`

### アプリカティブの便利な関数

Control.Applicative には liftA2 という、以下のような型を持つ関数がある。

```haskell
liftA2 :: (Applicative f) => (a -> b -> c) -> f a -> f b -> f c
```

liftA2 の定義は以下。

```haskell
liftA2 :: (Applicative f) => (a -> b -> c) -> f a -> f b -> f c
liftA2 f a b = f <$> a <*> b
```

通常のファンクターでは、関数を１つのファンクター値に適用することしかできない。
アプリカティブファンクターなら、関数をいくつものファンクター値に適用できる。

liftA2 は「通常の2引数関数を、2つのアプリカティブ値を引数に取る関数に昇格させる」関数とみなすことができる。
好きなアプリカティブ値たちから、それらの返り値をリストにしたものを持つ単一のアプリカティブ値を組み立てることは常に可能。

「アプリカティブ値のリスト」を取って「リストを返り値として持つ１つのアプリカティブ値」を返す関数を実装する。
そのなも sequenceA

```haskell
sequenceA :: (Applicative f) => [f a] -> f [a]
sequenceA [] = pure []
sequenceA (x:xs) = (:) <$> x <*> sequenceA xs
```

上記は下記のように記載するのと同じ。

```haskell
(:) <$> Just 1 <*> sequenceA [Just 2]
-- さらに評価をすすめるとこうなる
(:) <$> Just 1 ((:) <$> Just 2 <*> sequenceA [])
-- ここで sequenceA [] は Just [] になるので
(:) <$> Just 1 <*> ((:) <$> Just 2 <*> Just [])
-- こうなって...
(:) <$> Just 1 <*> Just [2]
-- Just [1,2] ができた
Just [1, 2]
```

sequenceA は畳み込みを使っても実装できる。リストの要素を集計するときは大抵畳み込みを使う。

```haskell
sequenceA :: (Applicative f) => [f a] -> f [a]
sequenceA = foldr (liftA2 (:)) (pure [])
```

Maybe 値のリストに対し sequenceA を使うと、すべての返り値のリストとして含んだ、  
単一の Maybe 値ができる。ただし、もとのリストの中の Maybe 値のいずれかが Nothing だと  
結果も Nothing になる。

sequenceA を関数にたいして使うと関数のリストを作ってくれる。

まとめるとアプリカティブファンクターは、面白い上にとても便利なもの。  
<$> と <*> を使うだけで、普通の関数をどんなアプリ化ティブとも組み合わせて使うことができる。

# Note

liftA2 の使い方。

```haskell
liftA2 (+) (Just 1) (Just 2)

-- 以下と同じ
(+) <$> (Just 1) <*> (Just 2)
```