# 13章 モナドがいっぱい

ファンクターは関数で写せる値を表す便利な概念。  
モナドはアプリカティブファンクターの強化版。

## 13.1 アプリカティブファンクターを強化する

アプリカティブ値は、変な値、専門用語でいうと「文脈の不可された値」だとみなせる。  
例えば、文字 a はただの文字だが、Just `a` は何らかの文脈がついている。

Applicative型クラスは、これらの文脈のついた値に、文脈を保ったまま普通の関数を適用させてくれる。

```haskell
(*) <$> Just 2 <*> Just 8

(++) <$> Just "exdeath" <*> Nothing

(-) <$> [3,4] <*> [1,2,3]
```

モナドはある願いを叶えるための、アプリカティブ値の自然な拡張である。  
その願いとは、「普通の値aを取って文脈付きの値を返す関数に、文脈付きの値m aを渡したい」というもの。

下記のような関数が欲しいということ。

```haskell
(>>=) :: (Monad m) => m a -> (a -> m a) -> m b
```

mはモナドのmで文脈を示す。`>>=`はバインドと呼ばれる。

## 13.2 Maybeから始めるモナド


```haskell
-- functor としての動作
fmap (++"!") (Just "window")
fmap (++"!") Nothing

-- applicative としての動作: 値に適応する関数にも文脈がつく
Just (+3) <*> Just 3

Nothing <*> Just "greed"

Just ord <*> Nothing

-- applicative style
max <$> Just 3 <*> Just 6

max <$> Just 3 <*> Nothing
```

`Maybe a` 型の値と `a -> Maybe b` 型の関数を取り、この関数をどうにかして `Maybe a` に適用する。
Maybeがアプリカティブファンクターであるという知識が役に立つ。

`(\x -> Just (x + 1)) 1` は数を取って結果を Just に包む関数。

```haskell
ghci>(\x -> Just (x + 1)) 1
Just 2
ghci>(\x -> Just (x + 1)) 100
Just 101
```

上記の関数にMaybeを食わせるようにしたい。

- Just値が来たときは、Justの中身を取り出してそれを関数に食わせる
- Nothing値が来たときは、関数はあるものの、それを適用すべき値がナッシングというわけなので、結果も Nothing にする

`>>=` とよぶのはやめて `applyMaybe` という名前にする。
これは「Maybe a型の値」と「Maybe bを返す関数」を引数に取り、どうにかしてその関数を「Maybe a」に適用してくれる関数。

```haskell
applyMaybe :: Maybe a -> (a -> Maybe b) -> Maybe b
applyMaybe Nothing f = Nothing
applyMaybe (Just x) f = f x

-- usage
ghci>Just 3 `applyMaybe` \x -> Just (x + 1)
Just 4
ghci>Just "smile" `applyMaybe` \x -> Just (x ++ " :)")
Just "smile :)"
ghci>Nothing `applyMaybe` \x -> Just (x + 1)
Nothing
ghci>Nothing `applyMaybe` \x -> Just (x ++ " :)")
Nothing
```

Just値と関数を引数に applyMaybe を呼び出したときは、単にJustの中の値に関数が適用されている。
Nothing値と関数を引数に呼び出すと、全体の結果がNothingになる。

関数のほうが、Nothingを返す場合は？

```haskell
ghci>Just 3 `applyMaybe` \x -> if x > 2 then Just x else Nothing
Just 3
ghci>Just 1 `applyMaybe` \x -> if x > 2 then Just x else Nothing
Nothing
```

applyMaybe の左辺のモナド値が Nothing だったら全体の結果は Nothing となる。
Maybe をアプリカティブとして使ったときの挙動に似ている。式のどこかに Nothing があったら結果も Nothing になるという挙動だ。

アプリカティブにできることはモナドにもできる。
アプリカティブにできないことがモナドにできることもある。

## 13.3 Monad型クラス

```haskell
class Monad m where
  return :: a -> m a

  (>>=) :: m a -> (a -> m b) -> m b
  (>>) :: m a -> m b -> m b
  x >> y = x >>= \_ -> y

  fail :: String -> m a
  fail msg = error msg
```

1つめのメンバ関数 `return` は Applicative#pure みたいなもん。
値を取ってデフォルトの文脈に入れる。

`>>=` はバインドと呼ばれる。
モナド値を取って、それに「通常の値を取るがモナド値を返す」関数を適用する。

`>>` 文脈を持った値を２つとって左辺の値で上書きする？

最後は `fail` 関数。ユーザーが使うことは滅多になく、もっぱら Haskell システムが呼び出す。
失敗をモナドの文脈で扱えるようにする。

```haskell
ghci>return "WHAT" :: Maybe String
Just "WHAT"
ghci>Just 9 >>= \x -> return (x*10)
Just 90
ghci>Nothing >>= \x -> return (x*10)
Nothing
```

## 13.4 綱渡り

### ひたすらコーディング

バランス棒は、単に整数のペアとして表現できる。ペアの第一成分は左側にいる鳥の数を、第二成分は左側にいるとりの数を表す。

まず、Int型の型シノニムを作り、Birdsと名付ける。
これはそこにいる鳥の数を表す整数。それから（Birds, Birds）の方シノニムを Pole と名付ける。

```haskell
type Birds = Int
type Pole = (Birds, Birds)
```

鳥の数を取って、バランス棒の左側もしくは右側に鳥を止まらせる関数を作る。

```haskell
landLeft :: Birds -> Pole -> Pole
landLeft n (left, right) = (left + n, right)
landRight :: Birds -> Pole -> Pole
landRight n (left, right) = (left, right + n)
```

試していく。

```haskell
ghci>landLeft 2 (0,0)
(2,0)
ghci>landLeft 1 (1,2)
(2,2)
ghci>landRight 1 (1,2)
(1,3)
ghci>landRight (-1) (1,2)
(1,1)
```

鳥を飛び立たせる処理は、負の数の鳥が止まる処理で代用する。
Pole に鳥を止まらせる関数は Pole を返すので、好きなだけ合成できる。

landLeft 1 を (0, 0) に適用すると (1, 0) になる。
それから右側に鳥が1羽止まったら、(1, 1) になる。さらに左側に鳥が2羽とまったら、
(3, 1) になる。

バランス棒を先に書いたほうが読みやすそうなので以下のような関数を作成する。

```haskell
x -: f = f x
```

これで関数を適用するのに、まず引数、次に関数を書けるようになった。

```haskell
ghci>100 -: (*3)
300
ghci>True -: not
False
ghci>:t not
not :: Bool -> Bool
ghci>not False
True
ghci>(0, 0) -: landLeft 2
(2,0)
```

この形式を使えば、鳥を次々と止まらせる処理をさっきより読みやすく書ける。

```haskell
ghci>(0, 0) -: landLeft 1 -: landRight 1 -: landLeft 2
```

こちらのほうが直感的。
(0, 0) から初めて、1羽の鳥が左に、1羽が右に、そして2羽が左にとまったことがすぐにわかる。

ポールに止まった鳥が左右で3羽以上差があった場合にピエールが綱渡りに失敗する実装を入れる、Maybe を使う。

```haskell
landLeft :: Birds -> Pole -> Maybe Pole
landLeft n (left, right)
    | abs ((left + n) - right) < 4 = Just (left + n, right)
    | otherwise                    = Nothing

landRight :: Birds -> Pole -> Maybe Pole
landRight n (left, right)
    | abs (left - (right + n)) < 4 = Just (left, right + n)
    | otherwise                    = Nothing
```

landLeft 関数、landRight 関数は、Pole でなくて Maybe Pole を返すようになった。  
鳥の数と、更新前のバランス棒の状態を引数にとるのは以前と同様だが、たくさん鳥が来たときはバランスを失った  
ピエールを放り出す検査が入るようになった。このコードでは、ガード記法を使って、更新後の左右の鳥の数の差が  
4より小さいかを判定している。

もし、4より小さいなら、新しいバランス棒の状態を Just に包んで返す。差が4以上になった場合は失敗を意味する  
Nothing を返す。

```haskell
ghci>landLeft 2 (0, 0)
Just (2,0)
ghci>landLeft 10 (0, 3)
Nothing
```

### ロープの上のバナナ

いきなりピエールを滑らせて落とす関数を作る。  
この関数をバナナと呼ぶ。

```haskell
banana :: Pole -> Maybe Pole
banana _ = Nothing
```

この関数は鳥をとまらせる関数と混ぜて使える。  
banana は引数に何を渡されようと、無視して失敗を返すようにできているので、  
banana を呼べばかならずピエールを落っことせる。

```haskell
ghci>return (0,0) >>= landLeft 1 >>= banana >>= landRight 1
Nothing
```

上の例では、banana に渡るのは Just (1, 0) というかなり良いバランスの値だが、banana はお構いなしに Nothing を返すので、以降すべての結果は Nothing になってしまう。残念でした。

ところで、入力に関係なく既定のモナド値を返す関数だったら、自作せずとも >> 関数を使うという手がある。これが >> のデフォルトの実装

```haskell
(>>) :: (Monad m) => m a -> m b -> m b
m >> n = m >>= \_ -> n
```

普通の関数なら、引数を無視して既定の値を返すような関数の結果は、その既定値そのものである。ところが、モナド値を扱う場合は、モナドとしての文脈と意味を考慮する必要がある。Maybe版の >> の動作は、こんな感じ。

```haskell
ghci>Nothing >> Just 3
Nothing
ghci>Just 3 >> Just 4
Just 4
ghci>Just 3 >> Nothing
Nothing
```

1番目の例では、Maybeモナドの文脈が考慮された結果、「規定値」であるはずの Just 3 が失敗に置き換わっている。>> を >>= \_ -> で置き換えてみれば何が起こっているのか簡単に理解できる。

`>>=` で連結した処理中での banana 関数は、>> と Nothing という、失敗することが保証された組み合わせで置き換えられる。

```haskell
ghci>return (0, 0) >>= landLeft 1 >> Nothing >>= landRight 1
Nothing
```

ところで、Maybe を失敗の文脈付きの値として扱って関数に食わせるという賢明な選択をしなかったら、どうなっていただろうか？

バランス棒に鳥をとまらせる一連の処理はこのようになったはずです。

```haskell
routine :: Maybe Pole
routine = case landLeft 1 (0, 0) of
    Nothing -> Nothing
    Just pole1 -> case landRight 4 pole1 of
        Nothing -> Nothing
        Just pole2 -> case landLeft 2 pole2 of
            Nothing -> Nothing
            Just pole3 -> landLeft 1 pole3
```

成功していた場合は、右側に鳥をとめる処理に進み、また場合分けをして...、  
という繰り返し。この巨大で見にくいコードを、>>= による素敵なモナド適用の連鎖で書き直すのは、Maybeモナド普及コードの定番。Maybeもなどを使うと、失敗するかもしれない処理が連続するコード簡潔に書くことができる。

## 13.5 do記法

Haskell にとってモナドはとても便利なので、モナド専用構文まで用意されている。  
その名はdo記法。do記法はすでに第8章で複数のI/Oアクションを1つに糊付けするときに使った。

実はdo記法はIOモナドだけじゃなくあらゆるモナドに使える。といっても基本は同じで、do記法は複数のモナド値を糊付けするもの。

```haskell
ghci>Just 3 >>= (\x -> Just (show x ++ "!"))
Just "3!"
```

これはモナド値を関数に渡して、モナド値が返ってという、何の変哲もなコード。  
このコードを実行すると、ラムダ式の中の x に 3 が入る。このラムダ式の中には、3はモナド値でなく通常の値として扱える。ではこのラムダ式の中にもう一つ `>>=` があったらどうなるか？

```haskell
ghci>Just 3 >>= (\x -> Just "!" >>= (\y -> Just (show x ++ y)))
Just "3!"
```

`>>=` の入れ子構造。外側のラムダ式の中では、Just "!" をラムダ式 `\y -> Just (show x ++ y)` に食わせている。このラムダ式では y は ! になる。また、x は、外側のラムダ式が実行されたときに 3 が入ったまま。これを見ていると let 構文を思い出す。

```haskell
ghci>let x = 3; y = "!" in show x ++ y
"3!"
```

この2つの例は似ているが、`>>=` のほうが使っている値はモナド値であるという大きな違いがある。  
失敗の文脈付きの値である。なので、好きな箇所を失敗で置き換えることができる。

```haskell
ghci>Nothing >>= (\x -> Just "!" >>= (\y -> Just (show x ++ y)))
Nothing

ghci>Just 3 >>= (\x -> Nothing >>= (\y -> Just (show x ++ y)))
Nothing

ghci>Just 3 >>= (\x -> Just "!" >>= (\y -> Nothing))
Nothing
```

let式で値を変数に束縛する操作に似ている。しかし操作したい値がモナド値であるという違いがある。  
この類似をもっと明確にするために、今の式をスクリプト風に書き直す。Maybe値ごとに1行を使う。

```haskell
foo :: Maybe String
foo = Just 3 >>= (\x ->
      Just "!" >>= (\y ->
      Just (show x ++ y)))
```

いちいち Maybe 値が Just か Nothing かなんで場合分けせずとも、Maybe 値から生の値が取り出せる気分になれる。もし中身を取り出そうとする値のいずれかが Nothing だったら、do式全体も Nothing になる。この do 式の中では、Maybe モナドから (存在するとおぼしき)値を取り出しつつ、値につきまとう文脈の処理は >>= に任せている。モナド値を連結させた式と等価なものを、ずっと簡潔に表せる記法が do 式。

### do 自由自在

```haskell
ghci>Just 9 >>= (\x -> Just (x > 8))
Just True
```

```haskell
marySue :: Maybe Bool
marySue = do
      x <- Just 9
      Just (x > 8)
```

### 帰ってきたピエール

do式を使う場合は各行がモナド値である必要があるので、直前の Pole の状態に名前をつけて landLeft 関数や landRight 関数に明示的に渡さなければならない。

Maybe 値の中身を調べてみれば、start には (0, 0)、first には (2, 0) が入ってくるはず。

```haskell
routine :: Maybe Pole
routine = do
      start <- return (0, 0)
      first <- landLeft 2 start
      second <- landRight 2 first
      landLeft 1 second
```

ピエールにバナナの皮を踏ませたい場合、do記法ではこう書く。

```haskell
routine :: Maybe Pole
routine = do
      start <- return (0, 0)
      first <- landLeft 2 start
      Nothing
      second <- landRight 2 first
      landLeft 1 second
```

ピエールの例では >>= を使うほうが良いと著者は思っている。理由は、各モナドが直前のモナドの結果に依存しているから。do記法では、どの時点のバランス棒に鳥を止まらせるのかを明示的に書かなければならない。常に１つ前のステップの結果を使うのであれば do を使うまでもないと思っている。

### パターンマッチと失敗

```haskell
justH :: Maybe Char
justH = do
    (x:xs) <- Just "hello"
    return x
```

do式の中でパターンマッチが失敗した場合、Monad型のクラスの一員であるfail関数が使われるので、異常終了という形ではなくそのモナドの文脈に合った形で失敗を処理できる。failのデフォルトの実装は以下のようになっている。

```haskell
fail :: (Monad m) => String -> m a
fail msg = error msg
```

デフォルトでは、failはプログラムを以上終了させる。  
Maybeのfailの実装は以下のような感じになっている。

```haskell
fail _ = Nothing
```

エラーメッセージを無視してNothingを作っている。

```haskell
wopwop :: Maybe Char
wopwop = do
    (x:xs) <- Just ""
    return x
```

このパターンマッチは失敗するので、その効果の失敗するパターンマッチのある行全体をNothingで置き換えたのと同じ。

## 13.6 リストモナド

これまでのところ、Maybe は失敗の文脈のついた値として解釈できること、>>=を使ってMaybe値を関数に食わせることで失敗処理を簡単に記述できることを見てきた。

この節では、リストのモナドとしての側面を使うことで、非決定性を伴うコードをきれいに読みやすく書く方法を見ていく。

第11章では、アプリカティブとしてのリストは非決定性計算を表すと言った。

例えば、5という値は決定的。つまり同評価してもただ1つの決まった計算結果にしかならない。その値は既知。一方、`[8,9,3]`のような値は複数の計算結果を含んでいるとも、複数の候補値を同時に重ね合わせたような1つの値であることも解釈できる。リストをアプリカティブ・スタイルで使うと、非決定性を表現していることがはっきりする。

```haskell
ghci>(*) <$> [1,2,3] <*> [10,100,1000]
[10,100,1000,20,200,2000,30,300,3000]
```

左辺のリストの要素と右辺リストの要素の、すべてのあり得る組み合わせの積が、答のリストに含まれている。非決定性計算ではたくさんの選択肢にでくわすが、その都度すべてを試す。すると最終結果はもっとたくさんの候補値を含んだ非決定的値になるわけである。

この日決定性計算という文脈は、うまくモナドに焼き直すことができる。リストの Monad インスタンスは、以下のように書ける。

```haskell
instance Monad [] where
    return x = [x]
    xs >>= f = concat (map f xs)
    fail _ = []
``` 

return は pure と同じで、引数の値が1つだけ入ってくるようなリストを作って返す。  
`>>=` は「文脈付きの値(モナディックな値)」を、「通常の値を取って文脈付きの値を返す関数」に食わせる演算。

```haskell
ghci>[3,4,5] >>= \x -> [x,-x]
[3,-3,4,-4,5,-5]
```

Maybe に >>= を使うと、失敗の可能性を考慮しながあら、モナド値を関数に供給できた。  
こんどはモナドが非決定性計算を処理してくれている。

[3,4,5] は非決定的値であり、それを食わせている関数も非決定的値を返すようになっている。  
最終結果も非決定的であり、リスト [3,4,5] から１つの要素を選んで \x -> [x, -x] に食わせる  
すべての場合を尽くす。関数の方も、数を取って2通りの答を返す。1つはそのまま、1つは符号を変えて。  
`>>=` を使ってさっきのリストをこの関数に食わせると、すべての数が両方の符号になって出てくる。  
ラムダ式の x は、リストの中の値を順に取る。この答がどうして得られるとか、実装を追いかけてみる。  
はじめに [3,4,5] がある。それをラムダ式で写すと以下のようになる。

```haskell
[[3,-3],[4,-4],[5,-5]]
```

ラムダ式がそれぞれの要素に作用した結果、二重リストができる。そしてこのリストは concat されて、非決定性関数が非決定的値に適用できた。

[] は、返すべき値が何もない、ということなので、Nothing とよく似ている。なので、失敗は空リストで表せれば良い。  
Maybe と同様、fail のエラーメッセージは無視される。

```haskell
ghci>[] >>= \x -> ["bad","mad","rad"]
[]
ghci>[1,2,3] >>= \x -> []
[]
```

Maybe のとき同様、 >>= を使えばリストをいくつでも連結して非決定性を伝搬させることができる。

```haskell
ghci>[1,2] >>= \n -> ['a','b'] >>= \ch -> return (n, ch)
[(1,'a'),(1,'b'),(2,'a'),(2,'b')]
```

上記の例を do 記法で書き直す場合は以下のようになる。

```haskell
listOfTuples :: [(Int, Char)]
listOfTuples = do
    n <- [1,2]
    ch <- ['a','b']
    return (n, ch)
```

モナド値から要素を取り出して普通の値であるかのように扱えている。  
そして裏では >>= が文脈の面倒を見てくれている。この場合の文脈は非決定性である。

### do記法とリスト内包表記

リストの do 記法を見てピンと来たあなた、これを見て。

```haskell
ghci>[(n,ch)|n<-[1,2],ch<-['a','b']]
[(1,'a'),(1,'b'),(2,'a'),(2,'b')]
```

そう、リスト内包表記である。do記法では、n は [1,2] の中の値を順に取り、その各々に対して ch は ['a','b']のどれかになり、  
そして最後の行は (n,ch)をデフォルト文脈(単一要素リスト、非決定性が増えない)に入れる。リスト内包表記でも同じことが起こっているが、最後の (n,ch) を return で  包む操作は、リスト内包表記の出力パートがやってくれているので省かれている。リスト内包表記も do 記法も、内部では >>= を使った非決定性計算に変換されている。

### MonadPlus と guard 関数

リスト内包表記では、出力する要素を選別(filter)することができた。例えば7の付く数だけを選んで出すには、

```haskell
ghci>[x|x<-[1..50],'7' `elem` show x]
[7,17,27,37,47]
```

x を show で文字に変え、それから文字 '7' が含まれているか調べる。  
この選別はどんなリストモナドに翻訳されるかを知るには guard関数とMonadPlus型クラスを学ぶ必要がある。

MonadPlus は、モノイドの性質をあわせ持つモナドを表す型クラスである。

```haskell
class Monad m => MonadPlus m where
    mzero :: m a
    mplus :: m a -> m a -> ma
```

mzero は、Monoid 型クラスでいう mempty に対応する概念、mplus は mappend に対応する。  
リストはモノイドでもあるので、MonadPlusのインスタンスにできる。

```haskell
instance MonadPlus [] where
    mzero = []
    mplus = (++)
```

リストに関する mzero は、候補が1つもない、失敗した非決定性計算を表している。  
mplus は2つの非決定的値を1つの値にくっつける。guard関数の定義は以下のようになっている。

```haskell
guard :: (MonadPlus m) => Bool -> m ()
guard True = return
guard False = mzero
```

- guard は真理値を引数に取る
- 引数がTrueなら、guardは () を成功を表す最小限の文脈に入れる
- 引数がFalseなら、guardは失敗したモナド値を作る

```haskell
ghci>guard (5 > 2) :: Maybe ()
Just ()
ghci>guard (1 > 2) :: Maybe ()
Nothing
ghci>guard (5 > 2) :: [()]
[()]
ghci>guard (1 > 2) :: [()]
[]
```

リストモナドでは guard を使って解の候補をふるい落とすことができる。

```haskell
ghci>[1..50] >>= (\x -> guard ('7' `elem` show x) >> return x)
[7,17,27,37,47]

-- guard 関数を >> につなぐと以下のようなことが起こる
ghci>guard (5 > 2) >> return "cool" :: [String]
["cool"]
ghci>guard (1 > 2) >> return "cool" :: [String]
[]
```

- guard が成功すれば、空のタプルが入ったモナドが返ってくる
- そこですかさず >> を使えば、その空タプルを無視し、何か別のものを返すことができる
- guard が失敗したら、後ろの return もつられて失敗する
- 空リストを >>= に食わせたら、答は必ず空リストになるため

guard はぶっちゃけ「引数が False なら直ちに失敗を投げよ、True ならダミーの値 () が入っている成功を作れ」と言っている。  
guard がやっているのは、計算を続けてよいかの判断。

do記法で書き直すと以下のようになる。

```haskell
sevensOnly :: [Int]
sevensOnly = do
    x <- [1..50]
    guard ('7' `elem` show x)
    return x
```

### 騎士の旅

```haskell
type KnightPos = (Int, Int)

moveKnight :: KnightPos -> [KnightPos]
moveKnight (c, r) = do
    (c', r') <- [(c+2,r-1),(c+2,r+1),(c-2,r-1),(c-2,r+1)
                ,(c+1,r-2),(c+1,r+2),(c-1,r-2),(c-1,r+2)]
    guard (c' `elem` [1..8] && r' `elem` [1..8])
    return (c', r')

ghci>moveKnight (6,2)
[(8,1),(8,3),(4,3),(7,4),(5,4)]
```

初期値を取って3手で行ける位置を返す関数。

```haskell
in3 :: KnightPos -> [KnightPos]
in3 start = do
    first <- moveKnight start
    second <- moveKnight first
    moveKnight second

ghci>in3 (6,2)
[(8,1),(8,3),(4,3),(7,4),(5,4),(5,1),(5,4),(8,1),(8,5),(6,1),(6,5),(8,2),(4,2),(7,3),(5,3),(8,3),(8,5),(4,2),(4,5),(7,2),(7,6),(5,2),(5,6),(5,2),(8,3),(6,3),(5,3),(5,6),(8,3),(8,7),(6,3),(6,7),(8,1),(8,3),(4,3),(7,4),(5,4),(8,3),(8,5),(4,2),(4,5),(7,2),(7,6),(5,2),(5,6),(4,2),(3,3),(1,3),(4,3),(4,5),(3,2),(3,6),(1,2),(1,6),(7,2),(3,2),(6,3),(4,3),(7,4),(7,6),(3,3),(3,6),(6,3),(6,7),(4,3),(4,7),(5,2),(1,2),(4,3),(2,3),(5,4),(5,6),(1,3),(1,6),(4,3),(4,7),(2,3),(2,7),(7,1),(7,3),(3,3),(6,4),(4,4),(7,4),(7,6),(3,3),(3,6),(6,3),(6,7),(4,3),(4,7),(6,3),(7,4),(6,4),(6,7),(7,4),(7,8),(8,1),(8,3),(4,3),(7,4),(5,4),(8,5),(8,7),(4,4),(4,7),(7,4),(7,8),(5,4),(5,8),(5,1),(5,4),(8,1),(8,5),(6,1),(6,5),(5,3),(5,6),(8,3),(8,7),(6,3),(6,7),(5,1),(5,3),(1,3),(4,4),(2,4),(5,4),(5,6),(1,3),(1,6),(4,3),(4,7),(2,3),(2,7),(8,1),(8,3),(4,3),(7,4),(5,4),(8,5),(8,7),(4,4),(4,7),(7,4),(7,8),(5,4),(5,8),(6,1),(6,3),(2,3),(5,4),(3,4),(6,5),(6,7),(2,4),(2,7),(5,4),(5,8),(3,4),(3,8)]

-- do記法を使わずに書く場合
in3 start = return start >>= moveKnight >>= moveKnight >>= moveKnight
```

2つの位置を取って、1つ目の位置から2つ目の位置にちょうど3手で到達できるか教えてくれる関数

```haskell
canReachIn3 :: KnightPos -> KnightPos -> Bool
canReachIn3 start end = end `elem` in3 start
```

### 読者への演習問題

TODO: 始点と終点を与えるとどういう経路をたどればいいか教えてくれるよう上記の関すを改造せよ。

## 13.7 モナド則

ある型が Monad のインスタンスだからといって、実際にモナドであるわけではない。  
ある型が真にモナドであるためには、その型はモナド則を満たさねばならない。

### 左恒等性

第一のモナド則が言っているのは、return を使って値をデフォルトの文脈に入れたものを >>= を使って関数に食わせた結果は、  
単にその値にその関数を適用した結果と等しくなりなさいということである。

つまり `return x >>= f` と `f x` は等価であるということである

### 右恒等性

モナドの第二の法則は、>>= を使ってモナド値を return に食わせた結果は、元のモナド値と不変であるということ。  
`>>=` を使ってモナド値を関数に食わせるという式があった場合、関数のほうは普通の値を取ってモナド値を返す関数のはず。  
`return` も、型を見れば、そういう関数の一種であることがわかる。  

`return` は、値をその値を返せるような最小限の文脈に入れるもの。
これは、例えば Maybe モナドであれば決して失敗しないことを意味し、リストモナドあれば非決定性を増やさないことを意味する。

何種類かのモナドで試してみる。

```haskell
ghci>Just "move on up" >>= return
Just "move on up"
ghci>[1,2,3,4] >>= return
[1,2,3,4]
ghci>putStrLn "Wah!" >>= return
Wah!
```

リストの場合 >>= の実装は以下のようになっていた。

```haskell
xs >>= f = concat (map f xs)
```

1. `[1,2,3,4]` を return に注ぎ込む
2. return が `[1,2,3,4]` を写して `[[1],[2],[3],[4]]` ができる
3. このリストが concat され、元のリストへ戻る

左恒等性も右恒等性は、いずれも基本的に return の振る舞いに関する法則である。  
return はモナドシステムの中で、通常の値をモナド値に変える重要な役割を担っている。  
return が作り出すモナド値が最小限でない余計な文脈を持っていたら困る。

### 結合法則

第三のモナド則は、`>>=` を使ったモナド関数適用の連鎖があるとき、  
どの順序で評価しても結果は同じであるべき、というもの。

```haskell
(m >== f) >>= g
m >>= (\x -> f x >>= g)
```

上記がそれぞれ等価であるということである。
例を見て理解していく。

ピエールのバランス棒に鳥が止まりに来る様子をシミュレートした関数連鎖を題材とする。

```haskell
ghci>return (0,0) >>= landRight 2 >>= landLeft 2 >>= landRight 2
Just (2,4)
```

この例では、まず Just (0, 0) があって、それにモナド関数 landRight 2 を適用していく。  
その結果はまた別のモナドであり、それがまた次のモナド関数に束縛され、と続いていく。  
この式にあえて括弧をつければ、以下のようになる。

```haskell
ghci>((return (0, 0) >>= landRight 2) >>= landLeft 2) >>= landRight 2
Just (2,4)
```

このルーチンは以下のように書くこともできる。

```haskell
return (0, 0) >>= (\x ->
landRight 2 x >>= (\y ->
landLeft 2 y >>= (\z -> landRight 2 z)))
```

このようにモナド値をモナド関数に食わせる式を評価するとき、入れ子の順序はどうでもよく、  
ただ関数の意味だけが重要であるというの結合法則。

この法則を別の側面から見てみる。  
いま、f と g という名の2つの関数があるとする。

```haskell
(.) :: (b -> c) -> (a -> b) -> (a -> c)
f . g = (\x -> f (g x))
```

g の型が a -> b で、f の型が b -> c ならば、2つの関数を結合して、  
g の返り値を f の引数に渡すことで、a -> c 型の新しい関数を作れる。

この２つがモナド関数であったらどうなるか？  
２つの関数の返り値がモナド値であったら?

a -> m b 型の関数の返り値は、そのままでは b -> m c 型の関数に渡すことはできない。  
後者は通常の b 型を期待しているので、モナド値は受け取れないためである。

ところが、>>= を使うとこれが可能になる。

```haskell
(<=<) :: (Monad m) => (b -> m c) -> (a -> m b) -> (a -> m c)
f <=< g = (\x -> g x >>= f)

ghci>let f x = [x,-x]
ghci>let g x = [x*3,x*2]
ghci>f 4
[4,-4]
ghci>g 5
[15,10]
ghci>let h = f <=< g
ghci>h 3
[9,-9,6,-6]
```

こうしてモナド関数の合成ができるようになった。

これと結合法則にどのような関係があるか？  
モナド結合法則を関数合成の法則としてみた場合、`f <=< (g <=< h)` と `(f <=< g) <=< h` が等価であるという宣伝になっている。
モナドの演算子を入れ子にする順番はどうでもよいということの別の表現になっている。  

ここで、`<=<` を使って最初の2つの法則を書き直してみると、
左恒等性は任意のモナド関数 f に対して f <=< return は f と等価であるという宣言、
右恒等性は return <=< f もまた f と等価であるという宣言になっている。

こうしてみると、３つのモナド則は、f が通常の関数であった場合に (f . g) . h と  
f . (g . h) が等価であり、f . id は常に f と等しく、id . f もただの f であることと非常に似ている。

# note

非決定性計算: リストを引数に取って各要素に対して計算などを行い、それぞれの結果を返すような計算のこと
