# 第8章 入出力

## 8.1 不順なものと純粋なものを分離する

人間からコンピューターに与えるの一連の実行ステップでなく、あるものが何であるかの定義。  
関数は副作用を持つことを許されない。関数が同じ引数で2回呼ばれたら、必ず同じ結果が返る。

関数が状態を変更できない、例えばグローバル変数を更新したりできないのは好ましいこと。
なぜならプログラムについての推論が用意になるから。

純粋だからこそ、遅延評価、堅牢性、関数合成などを利用できたいりする。

## 8.2 Hello World!!!

### Haskell ファイルをコンパイル

`ghc --make helloworld.hs`

### Hello, World! -> 生成されたファイルを実行する

`./helloworl`
hello, world

### I/Oアクション

````haskell
ghci>:t putStrLn
-- putStrLn :: String -> IO ()
ghci>:t putStrLn "hello, world!"
-- putStrLn "hello, world!" :: IO ()
````

### 8.3 I/Oアクションどうしをまとめる

複数のIOアクションを糊付けして1つにするのに do 構文が使える。

```haskell
main = do
    putStrLn "Hello, what's your name?"
    name <- getLine
    putStrLn ("Hey " ++ name ++ ", you rock!")
```

コンパイルする

```haskell
$ ghc --make IOAction.hs
[1 of 1] Compiling Main             ( IOAction.hs, IOAction.o )
Linking IOAction ...
$ ./IOAction
Hello, what's your name?
takato
Hey takato, you rock!
```

2回実行したときに同じ結果を返す保証が無いものを純粋な関数ではない。

```haskell
main = do
    foo <- putStrLn "Hello, what's your name?"
    name <- getLine
    putStrLn ("Hey " ++ name ++ ", you rock!")
```

下記はIOアクションに別の名前をつけているだけ
結果を変数に束縛しているのとは異なる

```haskell
myLine = getLine
```

##### I/Oアクションの中で let を使う

do を使うときに let 構文を使って純粋な値を名前に束縛できる。

```haskell
import Data.Char

main = do
    putStrLn "What's your first name?"
    firstName <- getLine
    putStrLn "What's your last name"
    lastName <- getLine
    let bigFirstName = map toUpper firstName
        bigLastName = map toUpper lastName
    putStrLn $ "hey " ++ bigFirstName ++ " "
                      ++ bigLastName
                      ++ ", how are you?"
```

##### 逆順い表示する

一行ずつ単語を読み込んで逆さまにするプログラムを作成する。
空行を入力したら停止するようにする。

```haskell
main = do
    line <- getLine
    if null line
        then return ()
        else do
            putStrLn $ reverseWords line
            main

reverseWords :: String -> String
reverseWords = unwords . map reverse . words
```

Haskell での return は、純粋な値から I/O アクションを作る。
作られた I/O アクションは何もしない。

先程おプログラムでは、空行入力時に実行するために何らかの I/O アクションが必要だった。
それが return () と書いて何もしない I/O アクションを作る理由。

他の言語と異なり Haskell の return には I/O の do ブロックの実行を終わらせる働きはない。
以下のプログラムは最後の行まで事もなげに実行される。

```haskell
main = do
    return ()
    return "hahaha"
    line <- getLine
    return "blah blah blah"
    return 4
    putStrLn line
```

do プロックで I/O を行うときは、たいてい return を使うことになる。
というのも、何もしない I/O アクションを作る必要があったり、do ブロックの最後のアクションで作り出された結果を
I/O アクションの結果として帰したくない場合があったりするから。

違う結果を I/O アクションの返り値にしたいときは、return を使って do ブロックの最後に配置する。

### いくつかの便利な I/O 関数

##### putStr

文字列をコンソールに出力

```haskell
main = do
    putStr "Hey, "
    putStr "I'm "
    putStr "Andy!"
```

##### putChar

文字を受け取り、出力する。

```haskell
main = do
    putChar 't'
    putChar 'e'
    putChar 'h'
```

`putChar` を使って `putStr` を再帰的に定義できる。

```haskell
putStr :: String -> IO ()
putStr [] = return ()
putStr (x:xs) = do
    putChar x
    putStr xs
```

再帰の基底部を定義して、それから残りのケースを考える。  
この場合だと、最初に先頭の文字を出力して、それから残りの文字列を出力する。

##### print

Show のインスタンスの型の値を受け取って、それに show を適用して「文字列化」して、
それからその文字列を端末に出力する。

```haskell
main = do
    print True
    print 2
    print "haha"
    print 3.2
    print [3,4,3]
```

main を定義したときも ghci のプロンプトで評価したときもタイプして enter を押したときに
ghci が print を使っている。

##### when

I/O アクションを受け取って、Bool の値が True の場合には渡された I/O と同じものを返し、False の場合は、
return () を返す。

```haskell
import Control.Monad

main = do
    input <- getLine
    when (input == "SWORDFISH") $ do
        putStrLn input
```

if 文で記載するより楽に実装できる。

##### sequence

```haskell
main = do
    a <- getLine
    b <- getLine
    c <- getLine
    print [a,b,c]

```

次のように記載できる。

```haskell
main = do
    rs <- sequence [getLine, getLine, getLine]
    print rs

```

##### mapM

「リストに対しI/Oアクションを返す関数をマップし、それからシーケンスにする」という操作は頻出するので、
ユーティリティ関数 mapM と mapM_ が用意されている。

関数とリストを受け取ってリストに対して関数をマップして、それからシーケンスにする。  
mapM_ も同じことをするが、その後で結果を捨ててしまう。

```haskell
ghci>mapM print [1,2,3]
1
2
3
[(),(),()]
ghci>mapM_ print [1,2,3]
1
2
3
```

##### forever

受け取った I/Oアクションを永遠と繰り返すI/Oアクションを返す。
Control.Monad で定義されている。

```haskell
import Control.Monad
import Data.Char

main = forever $ do
    putStr "Give me some input: "
    l <- getLine
    putStrLn $ map toUpper l
```

##### forM

最初の引数がリストで、２番目がそのリストにマップする関数。

```haskell
import Control.Monad

main = do
    colors <- forM [1,2,3,4] $ \a -> do
        putStrLn $ "Which color do you associate with the number " ++ show a ++ "?"
        color <- getLine
        return color
    putStrLn "The colors that you associate with 1, 2, 3, and 4 are: "
    mapM_ putStrLn colors
```

forM はこう考えるとよい。

「このリストの各要素に対応する I/O アクションを作る。
それぞれの I/O アクションの動作は、アクションを作るのに使った要素に応じたものにできる。
最終的には、これらのアクションが実行された結果がなにかに束縛される。
※結果が必要なければ丸ごと捨ててしまうこともできる。」

### 8.5 I/Oアクションおさらい

I/O アクションというのは値である。Haskell の他の値とよく似ている。
関数の引数として渡すことができて、関数の結果として I/O アクションを返すことができる。

I/O アクションが特別なのは main 関数に実装したり、GHCi のプロンプトで評価されると、
それが実行されるというところ。画面に何かを表示したり、音楽をスピーカーから再生したりする。

どの I/O アクションも、実世界から取得ｓｈちえきたものを伝える結果を生成できる。
